/*

	Incident No.	: ONE-5508 Database changes and migration of data
	Responsible		: Adrian Chiorean
	Sprint			: Grundtal
	Date created	: 15.03.2017

	Description		: Add new IMAGES table. Add PICTUREID column to POSMENULINE and RBOSTORETABLE. Migrate existing data to new table
*/

USE LSPOSNET

-- CREATE TABLE IMAGES
IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'IMAGES')
BEGIN
	CREATE TABLE IMAGES (
		[PICTUREID] NVARCHAR(20) NOT NULL , 
		[PICTURE] VARBINARY(MAX) NULL,
		[DATAAREAID] NVARCHAR(4) NOT NULL CONSTRAINT [DF_IMAGES_DATAAREAID]  DEFAULT ('LSR'),
		[DESCRIPTION] NVARCHAR(200) NULL,
		[TYPEOFIMAGE] TINYINT NOT NULL,
		[BACKGROUNDSTYLE] NVARCHAR(20) NULL,
		CONSTRAINT [PK_IMAGES] PRIMARY KEY CLUSTERED
	(
		[PICTUREID] ASC,
		[DATAAREAID] ASC
	) WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
	) ON [PRIMARY]

END
GO

-- ADD COLUMN PICTUREID TO POSMENULINE
IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'POSMENULINE' AND COLUMN_NAME = 'PICTUREID')
BEGIN
	ALTER TABLE POSMENULINE ADD PICTUREID NVARCHAR(20) NULL
END
GO

-- ADD COLUMN PICTUREID TO RBOSTORETABLE
IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'RBOSTORETABLE' AND COLUMN_NAME = 'PICTUREID')
BEGIN
	ALTER TABLE RBOSTORETABLE ADD PICTUREID NVARCHAR(20) NULL
END
GO

-- MIGRATE DATA
IF NOT EXISTS(SELECT 1 FROM POSISINFO WHERE ID = 'IMAGE_MIGRATION')
BEGIN

	-- DECLARE VARIABLES
	DECLARE @NEWPICTUREID INT = 0

	DECLARE @ENTITYID NVARCHAR(20)
	DECLARE @PICTURE VARBINARY(MAX)
	DECLARE @DESCRIPTION NVARCHAR(100)
	DECLARE @BACKGROUNDSTYLE NVARCHAR(20)
	DECLARE @DATAAREAID NVARCHAR(4)
	DECLARE @SEQUENCE NVARCHAR(20)
	
	-- MIGRATE POS IMAGES
	DECLARE LAYOUTCURSOR CURSOR FOR
		SELECT l.LAYOUTID, i.PICTURE, l.NAME, l.DATAAREAID
		FROM POSISTILLLAYOUT l
		JOIN POSISIMAGES i ON i.PICTUREID = l.LOGOPICTUREID
		WHERE l.LOGOPICTUREID IS NOT NULL
		ORDER BY l.LOGOPICTUREID

	OPEN LAYOUTCURSOR

	FETCH FROM LAYOUTCURSOR INTO @ENTITYID, @PICTURE, @DESCRIPTION, @DATAAREAID

	WHILE @@FETCH_STATUS = 0
	BEGIN
		SET @NEWPICTUREID = @NEWPICTUREID + 1

		INSERT INTO IMAGES (PICTUREID, PICTURE, DESCRIPTION, TYPEOFIMAGE, DATAAREAID) VALUES (FORMAT(@NEWPICTUREID, '0########'), @PICTURE, @DESCRIPTION + ' - Logo', 2, @DATAAREAID)
		
		UPDATE POSISTILLLAYOUT SET LOGOPICTUREID = FORMAT(@NEWPICTUREID, '0########') WHERE LAYOUTID = @ENTITYID AND DATAAREAID = @DATAAREAID

		FETCH NEXT FROM LAYOUTCURSOR INTO @ENTITYID, @PICTURE, @DESCRIPTION, @DATAAREAID
	END

	CLOSE LAYOUTCURSOR
	DEALLOCATE LAYOUTCURSOR

	-- MIGRATE STORE IMAGES
	DECLARE STORECURSOR CURSOR FOR
	SELECT STOREID, NAME, RECEIPTLOGO, DATAAREAID
	FROM RBOSTORETABLE
	WHERE RECEIPTLOGO IS NOT NULL

	OPEN STORECURSOR

	FETCH FROM STORECURSOR INTO @ENTITYID, @DESCRIPTION, @PICTURE, @DATAAREAID

	WHILE @@FETCH_STATUS = 0
	BEGIN
		SET @NEWPICTUREID = @NEWPICTUREID + 1

		INSERT INTO IMAGES (PICTUREID, PICTURE, DESCRIPTION, TYPEOFIMAGE, DATAAREAID) VALUES (FORMAT(@NEWPICTUREID, '0########'), @PICTURE, @DESCRIPTION + ' - Receipt logo', 3, @DATAAREAID)
		UPDATE RBOSTORETABLE SET RECEIPTLOGO = NULL, PICTUREID = FORMAT(@NEWPICTUREID, '0########') WHERE STOREID = @ENTITYID AND DATAAREAID = @DATAAREAID

		FETCH NEXT FROM STORECURSOR INTO @ENTITYID, @DESCRIPTION, @PICTURE, @DATAAREAID
	END

	CLOSE STORECURSOR
	DEALLOCATE STORECURSOR

	-- MIGRATE POS MENU LINE IMAGES
	DECLARE MENULINECURSOR CURSOR FOR
	SELECT l.MENUID, ISNULL(NULLIF(l.DESCRIPTION,''), h.DESCRIPTION), l.PICTURE, IIF(l.USEHEADERATTRIBUTES = 0, l.STYLEID, h.STYLEID), l.DATAAREAID, l.SEQUENCE
	FROM POSMENULINE l
	JOIN POSMENUHEADER h ON h.MENUID = l.MENUID
	WHERE l.PICTURE IS NOT NULL

	OPEN MENULINECURSOR

	FETCH FROM MENULINECURSOR INTO @ENTITYID, @DESCRIPTION, @PICTURE, @BACKGROUNDSTYLE, @DATAAREAID, @SEQUENCE

	WHILE @@FETCH_STATUS = 0
	BEGIN
		SET @NEWPICTUREID = @NEWPICTUREID + 1

		INSERT INTO IMAGES (PICTUREID, PICTURE, DESCRIPTION, TYPEOFIMAGE, BACKGROUNDSTYLE, DATAAREAID) VALUES (FORMAT(@NEWPICTUREID, '0########'), @PICTURE, @DESCRIPTION, 1, @BACKGROUNDSTYLE, @DATAAREAID)
		UPDATE POSMENULINE SET PICTURE = NULL, PICTUREFILE = '', PICTUREID = FORMAT(@NEWPICTUREID, '0########') WHERE MENUID = @ENTITYID AND DATAAREAID = @DATAAREAID AND [SEQUENCE] = @SEQUENCE

		FETCH NEXT FROM MENULINECURSOR INTO @ENTITYID, @DESCRIPTION, @PICTURE, @BACKGROUNDSTYLE, @DATAAREAID, @SEQUENCE
	END

	CLOSE MENULINECURSOR
	DEALLOCATE MENULINECURSOR

	-- ADD NEW NUMBER SEQUENCE
	IF NOT EXISTS(SELECT 1 FROM dbo.NUMBERSEQUENCETABLE where DATAAREAID = 'LSR' and NUMBERSEQUENCE = 'IMAGES')
	BEGIN
		INSERT INTO dbo.NUMBERSEQUENCETABLE (NUMBERSEQUENCE,TXT,LOWEST,HIGHEST,FORMAT,INUSE,EMBEDSTOREID,CANBEDELETED,STOREID,DATAAREAID)
	VALUES ('IMAGES','Images',1,999999999,'#########',1,0,0,'HO','LSR')
		INSERT INTO dbo.NUMBERSEQUENCEVALUE (NUMBERSEQUENCE,NEXTREC,STOREID,DATAAREAID)
	VALUES ('IMAGES',@NEWPICTUREID + 1,'HO','LSR')
	END

	-- MARK MIGRATION AS DONE
	INSERT INTO POSISINFO ([ID], [TEXT]) VALUES ('IMAGE_MIGRATION', 'Image bank migration complete')
END
GO

IF EXISTS(SELECT * FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'POSISIMAGES')
	EXECUTE spDB_SetTableDescription_1_0 'POSISIMAGES', '[OBSOLETE, ONE-5508] Contains images used by the POS';
GO