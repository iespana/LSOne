/*
	Incident No.	: ONE-12528
	Responsible		: Hörður Kristjánsson
	Sprint			: Pizza Hut
	Date created	: 27.08.2020

	Description		: Refactoring the "send to station" enum on hospitality types and migrating old settings to the new settings
*/

USE LSPOSNET

-- MIGRATE DATA
IF NOT EXISTS(SELECT 1 FROM POSISINFO WHERE ID = 'HOSPPRINT_MIGRATION')
BEGIN
	DECLARE @RESTAURANTID NVARCHAR(20)
	DECLARE @SEQUENCE INT
	DECLARE @SALESTYPE NVARCHAR(20)
	DECLARE @STATIONPRINTING INT
	
	DECLARE HOSPITALITYTYPECURSOR CURSOR FOR
		SELECT RESTAURANTID,
			   SEQUENCE,
			   SALESTYPE,
			   STATIONPRINTING
		FROM HOSPITALITYTYPE
		WHERE STATIONPRINTING > 0
		ORDER BY SEQUENCE

	OPEN HOSPITALITYTYPECURSOR

	FETCH FROM HOSPITALITYTYPECURSOR INTO @RESTAURANTID, @SEQUENCE, @SALESTYPE, @STATIONPRINTING

	WHILE @@FETCH_STATUS = 0
	BEGIN		
		-- AtPosExit(1) becomes AtPosPaymentOrPosExit(2)
		IF @STATIONPRINTING = 1 
		BEGIN
			UPDATE HOSPITALITYTYPE SET STATIONPRINTING = 2 WHERE RESTAURANTID = @RESTAURANTID AND SEQUENCE = @SEQUENCE AND SALESTYPE = @SALESTYPE
		END
		-- AtPosPosting(2) becomes AtPosPayment(1)
		ELSE IF @STATIONPRINTING = 2
		BEGIN
			UPDATE HOSPITALITYTYPE SET STATIONPRINTING = 1 WHERE RESTAURANTID = @RESTAURANTID AND SEQUENCE = @SEQUENCE AND SALESTYPE = @SALESTYPE
		END
		 -- AtPosExitAndPosPosting(3), AtItemAdded(4), AtItemAddedOneDelay(5) and AlwaysPrintAll(6) simply decrement by 1
		ELSE IF @STATIONPRINTING >= 3
		BEGIN
			UPDATE HOSPITALITYTYPE SET STATIONPRINTING = (@STATIONPRINTING - 1) WHERE RESTAURANTID = @RESTAURANTID AND SEQUENCE = @SEQUENCE AND SALESTYPE = @SALESTYPE			
		END		 

		FETCH NEXT FROM HOSPITALITYTYPECURSOR INTO @RESTAURANTID, @SEQUENCE, @SALESTYPE, @STATIONPRINTING
	END

	CLOSE HOSPITALITYTYPECURSOR
	DEALLOCATE HOSPITALITYTYPECURSOR

	-- MARK MIGRATION AS DONE
	INSERT INTO POSISINFO ([ID], [TEXT]) VALUES ('HOSPPRINT_MIGRATION', 'Station printing migration complete')
END
GO