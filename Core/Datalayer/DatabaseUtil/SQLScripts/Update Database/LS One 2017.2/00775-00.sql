/*
	Incident No.	: ONE-6888
	Responsible		: Adrian Chiorean
	Sprint			: Pax
	Date created	: 02.06.2017

	Description		: Create POSUSERPROFILE table. Migrate existing data
*/

USE LSPOSNET

IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE COLUMN_NAME = 'USERPROFILE' AND TABLE_NAME = 'RBOSTAFFTABLE')
BEGIN
	ALTER TABLE RBOSTAFFTABLE ADD USERPROFILE NVARCHAR(20) NULL
END
GO

IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'POSUSERPROFILE')
BEGIN
	-- CREATE NEW TABLE
	CREATE TABLE POSUSERPROFILE (
		[PROFILEID] NVARCHAR(20) NOT NULL,
		[DATAAREAID] NVARCHAR(4) NOT NULL CONSTRAINT [DF_POSUSERPROFILE_DATAAREAID]  DEFAULT ('LSR'),
		[DESCRIPTION] NVARCHAR(60) NOT NULL,
		[MAXLINEDISCOUNTAMOUNT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[MAXDISCOUNTPCT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[MAXTOTALDISCOUNTAMOUNT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[MAXTOTALDISCOUNTPCT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[MAXLINERETURNAMOUNT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[MAXTOTALRETURNAMOUNT] NUMERIC(28,12) NOT NULL DEFAULT (0),
		[STOREID] NVARCHAR(20) NULL DEFAULT (''),
		[VISUALPROFILE] NVARCHAR(20) NULL,
		[LAYOUTID] NVARCHAR(20) NULL,
		[KEYBOARDCODE] NVARCHAR(20) NULL,
		[LAYOUTNAME] NVARCHAR(20) NULL,
		[OPERATORCULTURE] NVARCHAR(20) NULL
		CONSTRAINT [PK_POSUSERPROFILE] PRIMARY KEY CLUSTERED
	(
		[PROFILEID] ASC,
		[DATAAREAID] ASC
	) WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
	) ON [PRIMARY]

	-- MARK OLD COLUMS AS OBSOLETE
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXLINEDISCOUNTAMOUNT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXDISCOUNTPCT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXTOTALDISCOUNTAMOUNT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXTOTALDISCOUNTPCT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXLINERETURNAMOUNT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'MAXTOTALRETURNAMOUNT', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'STOREID', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'VISUALPROFILE', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'LAYOUTID', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'KEYBOARDCODE', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'LAYOUTNAME', '[Obsolete, ONE-6888]';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOSTAFFTABLE', 'OPERATORCULTURE', '[Obsolete, ONE-6888]';

	-- Migrate existing data
	DECLARE @NEWUSERPROFILEID INT = 0
	DECLARE @EXISTINGUSERPROFILEID NVARCHAR(20)

	DECLARE @STAFFID NVARCHAR(20)
	DECLARE @STOREID NVARCHAR(20)
	DECLARE @VISUALPROFILE NVARCHAR(20)
	DECLARE @LAYOUTID NVARCHAR(20)
	DECLARE @KEYBOARDCODE NVARCHAR(20)
	DECLARE @LAYOUTNAME NVARCHAR(20)
	DECLARE @OPERATORCULTURE NVARCHAR(20)
	DECLARE @MAXLINEDISCOUNTAMOUNT NUMERIC(28,12)
	DECLARE @MAXDISCOUNTPCT NUMERIC(28,12)
	DECLARE @MAXTOTALDISCOUNTAMOUNT NUMERIC(28,12)
	DECLARE @MAXTOTALDISCOUNTPCT NUMERIC(28,12)
	DECLARE @MAXLINERETURNAMOUNT NUMERIC(28,12)
	DECLARE @MAXTOTALRETURNAMOUNT NUMERIC(28,12)
	DECLARE @DATAAREAID NVARCHAR(4)

	DECLARE USERPROFILECURSOR CURSOR FOR
	SELECT [STAFFID], [STOREID], [MAXLINEDISCOUNTAMOUNT], [MAXDISCOUNTPCT], [MAXTOTALDISCOUNTAMOUNT], [MAXTOTALDISCOUNTPCT], 
	       [MAXLINERETURNAMOUNT], [MAXTOTALRETURNAMOUNT], [VISUALPROFILE], [LAYOUTID], [KEYBOARDCODE], [OPERATORCULTURE], [LAYOUTNAME], [DATAAREAID]
	FROM RBOSTAFFTABLE
		
	OPEN USERPROFILECURSOR

	FETCH FROM USERPROFILECURSOR INTO @STAFFID, @STOREID, @MAXLINEDISCOUNTAMOUNT, @MAXDISCOUNTPCT, @MAXTOTALDISCOUNTAMOUNT, @MAXTOTALDISCOUNTPCT, @MAXLINERETURNAMOUNT, 
									  @MAXTOTALRETURNAMOUNT, @VISUALPROFILE, @LAYOUTID, @KEYBOARDCODE, @OPERATORCULTURE, @LAYOUTNAME, @DATAAREAID
	
	WHILE @@FETCH_STATUS = 0
	BEGIN
		
		SET @EXISTINGUSERPROFILEID  = (SELECT PROFILEID FROM POSUSERPROFILE WHERE @MAXLINEDISCOUNTAMOUNT = MAXLINEDISCOUNTAMOUNT AND @MAXDISCOUNTPCT = MAXDISCOUNTPCT AND
																				@MAXTOTALDISCOUNTAMOUNT = MAXTOTALDISCOUNTAMOUNT AND @MAXTOTALDISCOUNTPCT = MAXTOTALDISCOUNTPCT AND
																				@MAXLINERETURNAMOUNT = MAXLINERETURNAMOUNT AND @MAXTOTALRETURNAMOUNT = MAXTOTALRETURNAMOUNT AND
																				@VISUALPROFILE = VISUALPROFILE AND @KEYBOARDCODE = KEYBOARDCODE AND @OPERATORCULTURE = OPERATORCULTURE AND
																				@LAYOUTID = LAYOUTID AND @STOREID = STOREID AND @DATAAREAID = DATAAREAID AND @LAYOUTNAME = LAYOUTNAME)

		IF(@EXISTINGUSERPROFILEID IS NULL)
			BEGIN
				SET @NEWUSERPROFILEID = @NEWUSERPROFILEID + 1
				
				INSERT INTO POSUSERPROFILE (PROFILEID, DATAAREAID, DESCRIPTION, MAXLINEDISCOUNTAMOUNT, MAXDISCOUNTPCT, MAXTOTALDISCOUNTAMOUNT, MAXTOTALDISCOUNTPCT, MAXLINERETURNAMOUNT, MAXTOTALRETURNAMOUNT, VISUALPROFILE, LAYOUTID, KEYBOARDCODE, OPERATORCULTURE, STOREID, LAYOUTNAME)
				VALUES (FORMAT(@NEWUSERPROFILEID, '0########'), @DATAAREAID, FORMAT(@NEWUSERPROFILEID, '0########'), @MAXLINEDISCOUNTAMOUNT, @MAXDISCOUNTPCT, @MAXTOTALDISCOUNTAMOUNT, @MAXTOTALDISCOUNTPCT, @MAXLINERETURNAMOUNT, @MAXTOTALRETURNAMOUNT, @VISUALPROFILE, @LAYOUTID, @KEYBOARDCODE, @OPERATORCULTURE, @STOREID, @LAYOUTNAME)

				UPDATE RBOSTAFFTABLE SET USERPROFILE = FORMAT(@NEWUSERPROFILEID, '0########') WHERE STAFFID = @STAFFID AND DATAAREAID = @DATAAREAID
			END
		ELSE
			BEGIN
				UPDATE RBOSTAFFTABLE SET USERPROFILE = @EXISTINGUSERPROFILEID WHERE STAFFID = @STAFFID AND DATAAREAID = @DATAAREAID
			END

		FETCH NEXT FROM USERPROFILECURSOR INTO @STAFFID, @STOREID, @MAXLINEDISCOUNTAMOUNT, @MAXDISCOUNTPCT, @MAXTOTALDISCOUNTAMOUNT, @MAXTOTALDISCOUNTPCT, @MAXLINERETURNAMOUNT, 
											   @MAXTOTALRETURNAMOUNT, @VISUALPROFILE, @LAYOUTID, @KEYBOARDCODE, @OPERATORCULTURE, @LAYOUTNAME, @DATAAREAID
	END

	CLOSE USERPROFILECURSOR
	DEALLOCATE USERPROFILECURSOR

	-- Create number sequence
	IF NOT EXISTS(SELECT 1 FROM dbo.NUMBERSEQUENCETABLE where DATAAREAID = 'LSR' and NUMBERSEQUENCE = 'USERPROFILE')
	BEGIN
		INSERT INTO dbo.NUMBERSEQUENCETABLE (NUMBERSEQUENCE,TXT,LOWEST,HIGHEST,FORMAT,INUSE,EMBEDSTOREID,CANBEDELETED,STOREID,DATAAREAID)
			VALUES ('USERPROFILE','User profiles',1,999999999,'#########',1,0,0,'HO','LSR')
		INSERT INTO dbo.NUMBERSEQUENCEVALUE (NUMBERSEQUENCE,NEXTREC,STOREID,DATAAREAID)
			VALUES ('USERPROFILE',@NEWUSERPROFILEID + 1,'HO','LSR')
	END
END
GO