/*

	Incident No.	: ONE-8356
	Responsible		: Adrian Chiorean
	Sprint			: cheng
	Date			: 28.02.2018

	Description		: Adding a unique ID column to INVENTITEMBARCODE table
*/

USE LSPOSNET
GO

IF NOT EXISTS(SELECT 1 FROM dbo.NUMBERSEQUENCETABLE where DATAAREAID = 'LSR' and NUMBERSEQUENCE = 'ITEMBARCODE')
BEGIN
	INSERT INTO dbo.NUMBERSEQUENCETABLE (NUMBERSEQUENCE,TXT,LOWEST,HIGHEST,FORMAT,INUSE,EMBEDSTOREID,CANBEDELETED,STOREID,DATAAREAID)
		VALUES ('ITEMBARCODE','ItemBarcodes',1,999999999,'IB#######',1,0,0,'HO','LSR')
	INSERT INTO dbo.NUMBERSEQUENCEVALUE (NUMBERSEQUENCE,NEXTREC,STOREID,DATAAREAID)
		VALUES ('ITEMBARCODE',1,'HO','LSR')
END
GO

CREATE TABLE #tempVariables
(
	COLUMNADDED BIT NOT NULL
)
GO

IF NOT EXISTS (SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'INVENTITEMBARCODE' AND COLUMN_NAME = 'ITEMBARCODEID')
BEGIN
	-- Add new column
	ALTER TABLE dbo.INVENTITEMBARCODE ADD ITEMBARCODEID NVARCHAR(80) NOT NULL CONSTRAINT DF_INVENTITEMBARCODE_ITEMBARCODEID DEFAULT ''
	INSERT INTO #tempVariables VALUES (1)
END
GO

IF ((SELECT TOP 1 COLUMNADDED FROM #tempVariables) = 1)
BEGIN
	-- Update new column with existing barcodes
	UPDATE dbo.INVENTITEMBARCODE SET ITEMBARCODEID = ITEMBARCODE

	-- Delete default constraint, no longer needed
	ALTER TABLE dbo.INVENTITEMBARCODE DROP CONSTRAINT DF_INVENTITEMBARCODE_ITEMBARCODEID

	-- Change primary key to new column
	DECLARE @primaryKey nvarchar(60) = '';
	SET @primaryKey = (SELECT TOP 1 CONSTRAINT_NAME FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS WHERE TABLE_NAME = 'INVENTITEMBARCODE' AND CONSTRAINT_TYPE = 'PRIMARY KEY')

	EXEC('ALTER TABLE dbo.INVENTITEMBARCODE DROP CONSTRAINT ' + @primaryKey)
	ALTER TABLE dbo.INVENTITEMBARCODE ADD PRIMARY KEY (ITEMBARCODEID, DATAAREAID)
END
GO

DROP TABLE #tempVariables
GO