/*
	Incident No.	: ONE-9076
	Responsible		: Simona Avornicesei
	Sprint			: Borg
	Date created	: 12.09.2018

	Description		: Link states from STATES table to countries ids in COUNTRY table. Also move format id from states level to country level.
*/

USE LSPOSNET

IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'COUNTRY' AND COLUMN_NAME = 'FORMATTERID')
BEGIN
	ALTER TABLE COUNTRY ADD FORMATTERID INT NOT NULL DEFAULT(0);

	EXECUTE spDB_SetFieldDescription_1_0 'COUNTRY', 'FORMATTERID', 'Address formatter for the current country. It must match with a defined value in ADDRESSFORMAT.FORMATTERID';

	INSERT INTO POSISINFO(ID, TEXT) VALUES ('00846-00_COUNTRY_TBL', 'v2');
END
GO

IF (SELECT [TEXT] FROM POSISINFO WHERE ID = '00846-00_COUNTRY_TBL') = 'v2'
BEGIN
	UPDATE COUNTRY SET FORMATTERID = 2 WHERE COUNTRYID = 'US';
	UPDATE COUNTRY SET FORMATTERID = 3 WHERE COUNTRYID = 'CA';
	UPDATE COUNTRY SET FORMATTERID = 4 WHERE COUNTRYID = 'IN';
END
GO

IF EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'COUNTRY' AND COLUMN_NAME = 'ADDRFORMAT')
BEGIN
	EXECUTE spDB_SetFieldDescription_1_0 'COUNTRY', 'ADDRFORMAT', '[Obsolete]';
END
GO



IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'STATES' AND COLUMN_NAME = 'COUNTRYID')
BEGIN
	ALTER TABLE STATES ADD COUNTRYID NVARCHAR(10);

	EXECUTE spDB_SetFieldDescription_1_0 'STATES', 'COUNTRYID', 'Country to which the current state record belongs.It must match with a defined value in COUNTRY.COUNTRYID';

	INSERT INTO POSISINFO(ID, TEXT) VALUES ('00846-00_STATES_TBL', 'v2');
END
GO

IF (SELECT [TEXT] FROM POSISINFO WHERE ID = '00846-00_STATES_TBL') = 'v2'
BEGIN
	UPDATE STATES SET COUNTRYID = 'US' WHERE FORMATTERID = 2;
	UPDATE STATES SET COUNTRYID = 'CA' WHERE FORMATTERID = 3;
	UPDATE STATES SET COUNTRYID = 'IN' WHERE FORMATTERID = 4;
END

IF EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'STATES' AND COLUMN_NAME = 'FORMATTERID')
BEGIN
	EXECUTE spDB_SetFieldDescription_1_0 'STATES', 'FORMATTERID', '[Obsolete]';
END
GO

IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'ADDRESSFORMAT')
BEGIN
	CREATE TABLE [dbo].[ADDRESSFORMAT](
		[FORMATTERID]		[INT] NOT NULL PRIMARY KEY,
		[NAME]				[NVARCHAR](50) NOT NULL,
		[ISSYTEMFORMAT]		[BIT] NOT NULL DEFAULT (0),
		[PATTERN]			[NVARCHAR](512)
	);

	EXEC spDB_SetTableDescription_1_0 'ADDRESSFORMAT','Contains the predefined and any other custom address layout';

	EXECUTE spDB_SetFieldDescription_1_0 'ADDRESSFORMAT', 'FORMATTERID', 'Address format id. The predefined ones (with ids from 0 to 4) are from source code (LSOne.Utilities.DataType.Address.AddressFormatEnum)';
	EXECUTE spDB_SetFieldDescription_1_0 'ADDRESSFORMAT', 'NAME', 'User-friendly name of the address format';
	EXECUTE spDB_SetFieldDescription_1_0 'ADDRESSFORMAT', 'ISSYTEMFORMAT', 'False if the current record is a custom, user-defined one and not a predefined in LSOne (like ''Generic with state'', ''Generic without state'', ''US'', ''Canadian'', ''Indian'')';
	EXECUTE spDB_SetFieldDescription_1_0 'ADDRESSFORMAT', 'FORMATTERID', 'Pattern of the address format';

	INSERT INTO ADDRESSFORMAT([FORMATTERID], [NAME], [ISSYTEMFORMAT]) VALUES (0, 'Generic with state', 1);
	INSERT INTO ADDRESSFORMAT([FORMATTERID], [NAME], [ISSYTEMFORMAT]) VALUES (1, 'Generic without state', 1);
	INSERT INTO ADDRESSFORMAT([FORMATTERID], [NAME], [ISSYTEMFORMAT]) VALUES (2, 'US', 1);
	INSERT INTO ADDRESSFORMAT([FORMATTERID], [NAME], [ISSYTEMFORMAT]) VALUES (3, 'Canadian', 1);
	INSERT INTO ADDRESSFORMAT([FORMATTERID], [NAME], [ISSYTEMFORMAT]) VALUES (4, 'Indian', 1);
END
GO

--cleanup
DELETE FROM POSISINFO WHERE [ID] IN ('00846-00_COUNTRY_TBL', '00846-00_STATES_TBL')
GO