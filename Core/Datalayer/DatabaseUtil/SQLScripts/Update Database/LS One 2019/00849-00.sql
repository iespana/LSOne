/*
	Incident No.	: ONE-8799
	Responsible		: Helgi Rúnar Gunnarsson
	Sprint			: Cardassians
	Date created	: 05.10.2018

	Description		: Add a PriceID to PriceDiscTable
*/

USE LSPOSNET

-- Add column

IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'PRICEDISCTABLE' AND COLUMN_NAME = 'PRICEID')
BEGIN
	ALTER TABLE PRICEDISCTABLE ADD PRICEID NVARCHAR(60) NULL

	EXECUTE spDB_SetFieldDescription_1_0 'PRICEDISCTABLE', 'PRICEID', 'ID generated from number sequence to be sent to ERP systems';

	INSERT INTO POSISINFO(ID, TEXT) VALUES ('00849-00_PRICE_TBL', 'v2');
END
GO

IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'RBOTRANSACTIONSALESTRANS' AND COLUMN_NAME = 'PRICEID')
BEGIN
	ALTER TABLE RBOTRANSACTIONSALESTRANS ADD PRICEID NVARCHAR(60) NULL
	ALTER TABLE RBOTRANSACTIONSALESTRANS ADD PRICETYPE INT NULL

	EXECUTE spDB_SetFieldDescription_1_0 'RBOTRANSACTIONSALESTRANS', 'PRICEID', 'ID generated from number sequence to be sent to ERP systems';
	EXECUTE spDB_SetFieldDescription_1_0 'RBOTRANSACTIONSALESTRANS', 'PRICETYPE', 'Type of the trade agreement. BasePrice = 0, SalesPrice = 1, Promotion = 2';
END
GO

IF (SELECT [TEXT] FROM POSISINFO WHERE ID = '00849-00_PRICE_TBL') = 'v2'
BEGIN
	UPDATE PRICEDISCTABLE
	SET PRICEID = '00000' + New_PRICEID
	FROM 
	(
		SELECT PRICEID, FORMAT(ROW_NUMBER() OVER (ORDER BY (SELECT NULL)), 'OG-00000') AS New_PRICEID
		FROM PRICEDISCTABLE
	) PRICEDISCTABLE

	WHERE PRICEID IS NULL

	ALTER TABLE PRICEDISCTABLE ALTER COLUMN PRICEID NVARCHAR(60) NOT NULL
END
GO

IF NOT EXISTS(SELECT * FROM sys.indexes WHERE NAME = 'LS_PRICEDISCTABLE_PRICEID')
BEGIN
	CREATE NONCLUSTERED INDEX LS_PRICEDISCTABLE_PRICEID
	ON PRICEDISCTABLE(PRICEID)
END
GO

--cleanup
DELETE FROM POSISINFO WHERE [ID] IN ('00849-00_PRICE_TBL')
GO
