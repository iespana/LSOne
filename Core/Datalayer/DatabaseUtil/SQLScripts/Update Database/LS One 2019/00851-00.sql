/*
	Incident No.	: ONE-7982
	Responsible		: Ovidiu Caba
	Sprint			: Edosian
	Date created	: 30.10.2018

	Description		: Adding columns CREATED and MODIFIED to RETAILITEM
*/

USE LSPOSNET

-- Add the columns

IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'RETAILITEM' AND COLUMN_NAME = 'CREATED')
BEGIN
	ALTER TABLE RETAILITEM ADD CREATED DATETIME2 NULL
	ALTER TABLE RETAILITEM ADD CONSTRAINT DF_RETAILITEM_CREATED DEFAULT GETDATE() FOR CREATED
END
GO

IF NOT EXISTS(SELECT * FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'RETAILITEM' AND COLUMN_NAME = 'MODIFIED')
BEGIN
	ALTER TABLE RETAILITEM ADD MODIFIED DATETIME2 NULL
	ALTER TABLE RETAILITEM ADD CONSTRAINT DF_RETAILITEM_MODIFIED DEFAULT GETDATE() FOR MODIFIED
END
GO

-- Import data from Audit only if data exists, otherwise we get an error when creating new databases

if((SELECT COUNT(*) FROM RETAILITEM) > 0) AND EXISTS (SELECT * FROM LSPOSNET_Audit.INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'RETAILITEMLog')	
BEGIN
	;DISABLE TRIGGER Update_RETAILITEM ON RETAILITEM

	;WITH CREATEDDATES
	AS
	(
		SELECT MASTERID, MIN(AUDITDATE)	AS AUDITDATE	-- SELECT MIN AUDITDATE, AS BEING THE DATE OF CREATION OF THE ROW
		FROM LSPOSNET_Audit.DBO.RETAILITEMLog
		GROUP BY MASTERID
	),
	MODIFIEDDATES
	AS
	(
		SELECT MASTERID, MAX(AUDITDATE)	AS AUDITDATE	-- SELECT MAX AUDITDATE, AS BEING THE DATE OF THE LAST MODIFICATION OF THE ROW
		FROM LSPOSNET_Audit.DBO.RETAILITEMLog
		GROUP BY MASTERID
	)
	UPDATE RI
	SET CREATED = ISNULL(CD.AUDITDATE, CAST('19000101' AS DATETIME2)),
		MODIFIED = ISNULL(MD.AUDITDATE, CAST('19000101' AS DATETIME2))
	FROM RETAILITEM RI
	LEFT JOIN CREATEDDATES CD ON RI.MASTERID = CD.MASTERID
	LEFT JOIN MODIFIEDDATES MD ON RI.MASTERID = MD.MASTERID
	WHERE RI.CREATED IS NULL AND RI.MODIFIED IS NULL

	;ENABLE TRIGGER Update_RETAILITEM ON RETAILITEM
END
GO

-- The columns should not be nullable

ALTER TABLE RETAILITEM
ALTER COLUMN CREATED DATETIME2 NOT NULL
GO

ALTER TABLE RETAILITEM
ALTER COLUMN MODIFIED DATETIME2 NOT NULL
GO